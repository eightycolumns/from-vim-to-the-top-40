#include "bus/main-mix"
#include "bus/short-reverb"
#include "oscillator/operator"
#include "plugin/gain"
#include "plugin/hi-pass"
#include "plugin/moog-ladder"
#include "util/amp-midi-velo"
#include "util/cps-midi-note"
#include "util/index"

instr Kick
  iDuration = p3
  iMidiVelo = p4
  iMidiNote = p5

  iDbRange = 24
  iAmp AmpMidiVelo iMidiVelo, iDbRange

  iMinMidiNote = 28
  iMaxMidiNote = 63
  iCps CpsMidiNote iMidiNote, iMinMidiNote, iMaxMidiNote

  kAmpEnv transeg 1, iDuration, -3, 0
  kCpsEnv transeg 4, iDuration, -9, 1
  kModEnv transeg 1, iDuration, -9, 0

  aPitchFeedback init 0
  aPitch Operator iCps * kCpsEnv, aPitchFeedback, 0.25
  aPitchFeedback = aPitch * Index(0.5) * kModEnv
  aPitch *= iAmp * kAmpEnv

  aNoiseFeedback init 0
  aNoise Operator 440, aNoiseFeedback, 0
  aNoiseFeedback = aNoise * Index(100)
  aNoise *= iAmp * kModEnv

  aNoise, aNoise MoogLadder aNoise, aNoise, 8000, 0.1

  aOut = aPitch + aNoise / 16

  aOut, aOut HiPass aOut, aOut, 40
  aOut, aOut Gain aOut, aOut, -3

  ToShortReverbBus aOut, aOut, -12
  ToMainMixBus aOut, aOut, 0
endin
